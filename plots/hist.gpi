#!/usr/bin/gnuplot

# terminal and png output
set terminal pngcairo size 1024,768 enhanced font "Monospace,10"
set output "plot.png"

# remove border on top and right and set color to gray
set style line 11 lc rgb "#808080" lt 1
set border 3 back ls 11
set tics nomirror

#define grid
set style line 12 lc rgb "#808080" lt 0 lw 1
set grid back ls 12

#color definitions
set style line 1 lc rgb "#cc0000" pt 1 ps 1 lt 1 lw 2 # red
set style line 2 lc rgb "#009999" pt 2 ps 1 lt 1 lw 2 # light blue
set style line 3 lc rgb "#00cc00" pt 3 ps 1 lt 1 lw 2 # green
set style line 4 lc rgb "#ffcc00" pt 4 ps 1 lt 1 lw 2 # yellow
set style line 5 lc rgb "#ff00cc" pt 5 ps 1 lt 1 lw 2 # fuscia
set style line 6 lc rgb "#ff9900" pt 6 ps 1 lt 1 lw 2 # orange
set style line 7 lc rgb "#003399" pt 7 ps 1 lt 1 lw 2 # blue

#labeling
set title "Overlap between maximal set" font "Monospace Bold, 10"
set xlabel "Overlap size"
set ylabel "Number of sets"

set key center right

plot "plot.txt" using 1:2 title "12" w lp ls 1, \
     ""         using 1:3 title "13" w lp ls 2, \
     ""         using 1:4 title "14" w lp ls 3, \
     ""         using 1:5 title "15" w lp ls 4, \
     ""         using 1:6 title "16" w lp ls 5, \
     ""         using 1:7 title "17" w lp ls 6, \
     ""         using 1:8 title "18" w lp ls 7
#
