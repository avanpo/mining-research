#!/usr/bin/gnuplot

# terminal and png output
set terminal pngcairo size 1024,768 enhanced font "Monospace,10"
set output "plot.png"

# remove border on top and right and set color to gray
set style line 11 lc rgb "#808080" lt 1
set border 3 back ls 11
set tics nomirror

#define grid
set style line 12 lc rgb "#808080" lt 0 lw 1
set grid back ls 12

#color definitions
set style line 1 lc rgb "#cc0000" pt 1 ps 1 lt 1 lw 2 # red
set style line 2 lc rgb "#009999" pt 2 ps 1 lt 1 lw 2 # light blue
set style line 3 lc rgb "#00cc00" pt 3 ps 1 lt 1 lw 2 # green

#labeling
set title "Support Difference for Neighboring Frequent Itemsets" font "Monospace Bold, 10"
set xlabel "Support Difference"
set ylabel "Number of Frequent Itemsets"

set key center right

plot "plot.txt" using 1:2 title "1" w lp ls 1, \
     ""         using 1:3 title "2" w lp ls 2, \
     ""         using 1:4 title "3" w lp ls 3
#
